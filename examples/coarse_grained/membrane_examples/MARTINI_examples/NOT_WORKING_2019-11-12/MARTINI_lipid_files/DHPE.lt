import "martini.lt"

DHPE inherits MARTINI {

  # atomID    molID  atomTyle   charge       X          Y          Z
  # (guessing the coordinates, and scaling them manually)
  write('Data Atoms') {
    $atom:NH3 $mol:. @atom:Qd   0.2236068   0.000000   0.000000   6.000000
    $atom:PO4 $mol:. @atom:Qa  -0.2236068   0.000000   0.000000   5.000000
    $atom:GL1 $mol:. @atom:Na   0.0         0.000000   0.000000   4.000000
    $atom:GL2 $mol:. @atom:Na   0.0         0.000000   1.000000   4.000000
    $atom:C1A $mol:. @atom:C1   0.0         0.000000  -1.000000   4.000000
    $atom:C2A $mol:. @atom:C1   0.0         0.000000  -1.000000   3.000000
    $atom:C1B $mol:. @atom:C1   0.0         0.000000   1.000000   3.000000
    $atom:C2B $mol:. @atom:C1   0.0         0.000000   1.000000   2.000000
  }
  # Note: The coordinates here are integers because we were using a text editor
  #       and we just wanted an easy way to create non-overlapping coordinates
  #       for the particles in these molecules.  Later we will use the
  #       .scale(4.7) command to convert these coordinates into Angstroms.
  #       (The shape of the molecule will be unrealistic, but we can fix
  #        that later during minimization.)
  # Note: The charge of the Q0 and Qa is 1/sqrt(20) to compensate for the fact
  #       that the MARTINI force field uses electrostatic interactions which
  #       in SI units is    q1*q2 / 4π ε0 ε
  #       ...where "ε0" is the vacuum permitivity, and "ε" is the
  #       relative permitivity of water used in the MARTINI model,
  #       WHICH THEY CHOSE TO BE 20.0
  #       (This is an odd choice.  In real water, at large distances ε≈80.)
  #       However the electrostatic interactions in most LAMMPS pair styles
  #       assume that ε=1.  To compensate for this, we divide charges by sqrt(ε)

  write('Data Bond List') {
    $bond:b1 $atom:NH3 $atom:PO4
    $bond:b2 $atom:PO4 $atom:GL1
    $bond:b3 $atom:GL1 $atom:GL2
    $bond:b4 $atom:GL1 $atom:C1A
    $bond:b5 $atom:C1A $atom:C2A
    $bond:b6 $atom:GL2 $atom:C1B
    $bond:b7 $atom:C1B $atom:C2B
  }
}


DHPE.scale(4.7)  # Convert lattice (integer) coordinates into Angstroms

# Optional: Move the molecule to a convenient location for buildling a bilayer:
#           Center the molecule in the X,Y directions and set the min Z value.

DHPE.move(-0,-0.5875,-7.15).rot(-30.0,0,0,1)
